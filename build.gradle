buildscript {
    ext {
        js2pVersion = '1.0.2'
        springfoxVersion = '2.8.0'
    }
    repositories {
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath("org.jsonschema2pojo:jsonschema2pojo-gradle-plugin:${js2pVersion}")
        classpath 'org.liquibase:liquibase-core:3.6.3'
        classpath "org.liquibase:liquibase-gradle-plugin:2.0.1"
    }
}

plugins {
    id 'org.springframework.boot' version '2.2.5.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'java'
    id "org.liquibase.gradle" version "2.0.1"
}
apply plugin: 'jsonschema2pojo'

group = 'pl.goreit'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

repositories {
    mavenCentral()
}

dependencies {
    implementation(
            'org.springframework.boot:spring-boot-starter-data-mongodb',
            'org.springframework.boot:spring-boot-starter-web',
            'org.springframework.boot:spring-boot-starter-aop',
            'org.springframework.boot:spring-boot-starter-data-jpa',
            "io.springfox:springfox-swagger2:${springfoxVersion}",
            "io.springfox:springfox-swagger-ui:${springfoxVersion}",
            'org.liquibase:liquibase-core:3.6.3',
            'org.postgresql:postgresql',
            
    )

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }

    testCompile group: 'com.h2database', name: 'h2', version: '1.4.200'

    // https://mvnrepository.com/artifact/ch.qos.logback/logback-classic
    runtime group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'

    liquibaseRuntime(
            'org.liquibase:liquibase-core:3.6.3',
            'org.liquibase:liquibase-groovy-dsl:2.0.1',
            'org.postgresql:postgresql',
            'ch.qos.logback:logback-core:1.2.3',
            'ch.qos.logback:logback-classic:1.2.3'
    )
}

test {
    useJUnitPlatform()
}

def cPath = "$rootDir" + "/src/main/resources"
def changeLog = 'db/liquibase-master-changelog.xml'
def dbDriver = 'jdbc:postgresql'
def dbName = 'goreit'

liquibase {
    activities {
        main {
            changeLogFile changeLog
            url "${dbDriver}://localhost:20002/${dbName}"
            username 'postgres'
            password 'Lideo123^'
            classpath "${cPath}"
        }
    }
    runList = project.hasProperty("runList") ? project.ext.runList : "main"
}

jsonSchema2Pojo {
    targetPackage = 'pl.goreit.api.generated'
    source = files('src/main/resources/json/')
    includeHashcodeAndEquals = true
    includeToString = true
    includeConstructors = true
    includeSetters = false
    initializeCollections = true
    targetVersion = 1.8
    inclusionLevel = 'NON_EMPTY'
    dateTimeType = "java.time.LocalDateTime"
    dateType = "java.time.LocalDate"
    timeType = "java.time.LocalTime"
    removeOldOutput = true
    useBigDecimals = true
    generateBuilders = true
}